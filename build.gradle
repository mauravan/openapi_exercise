import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
	id 'org.springframework.boot' version '2.7.2'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'java'
	id "org.openapi.generator" version "6.0.1"
	id "io.freefair.lombok" version "6.5.0.3"
}

group = 'ch.ti8m'
version = '0.0.1-SNAPSHOT'

tasks.jar.enabled = false

ext.inputSpec = "${rootDir}/openapi/customer-rest-api.yaml"
ext.outputSpec = "connector-api.json"

sourceSets {
	main {
		java {
			srcDir "${buildDir}/generated-client/src/main/java"
			srcDir "${buildDir}/generated-server/src/main/java"
		}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

tasks.compileJava.dependsOn("openApiGenerateServer")
tasks.compileJava.dependsOn("openApiGenerateClient")

tasks.withType(JavaCompile) {
	options.compilerArgs << '-parameters'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation 'com.h2database:h2'
	implementation 'org.flywaydb:flyway-core'

	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.3'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.3'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.0'
	implementation 'io.swagger.core.v3:swagger-models:2.2.0'
	implementation 'io.swagger.core.v3:swagger-core:2.2.0'
	implementation 'org.springdoc:springdoc-openapi-common:1.6.9'
	implementation 'org.springframework.data:spring-data-commons:2.7.0'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.2'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'org.hibernate:hibernate-validator:7.0.4.Final'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.3'

	implementation "io.swagger:swagger-annotations:1.6.6"
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

openApiValidate {
	inputSpec = rootProject.inputSpec
}

task openApiGenerateServer(type: GenerateTask) {
	dependsOn "openApiValidate"
	group = 'openapi tools'
	description = 'generate server'

	generatorName = "spring"
	library = "spring-boot"
	inputSpec = rootProject.inputSpec
	outputDir = "${buildDir}/generated-server"
	apiPackage = "ch.ti8m.api.endpoint"
	modelPackage = "ch.ti8m.connector.api.dto"
	modelNameSuffix = "Dto"

	configOptions = [
			dateLibrary             : "java8-localdatetime",
			delegatePattern         : "true",
			apiFirst                : "false",
			interfaceOnly           : "true",
			skipDefaultInterface    : "true",
			booleanGetterPrefix     : "is",
			developerOrganization   : "ti8m AG",
			performBeanValidation   : "true",
			useBeanValidation	    : "true",
			withXml                 : "true",
			openApiNullable         : "false",
			unhandledException		: "true"
	]
}

task openApiGenerateClient(type: GenerateTask) {
	dependsOn "openApiValidate"
	group = 'openapi tools'
	description = 'generate client'

	generatorName = "java"
	library = "resttemplate"
	inputSpec = rootProject.inputSpec
	outputDir = "${buildDir}/generated-client"
	apiPackage = "ch.ti8m.client.gateway"
	modelPackage = "ch.ti8m.connector.client.dto"
	modelNameSuffix = "Dto"

	configOptions = [
			dateLibrary             : "java8-localdatetime",
			booleanGetterPrefix     : "is",
			developerOrganization   : "ti8m AG",
			performBeanValidation   : "true",
			withXml                 : "true",
			openApiNullable         : "false"
	]
}

tasks.named('test') {
	useJUnitPlatform()
}
